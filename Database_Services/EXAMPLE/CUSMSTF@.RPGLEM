      *===============================================================
      *
      *  Copyright (c) 2008-2018 TEMBO Technology Lab (Pty) Ltd.
      *  Created by AO Foundation - www.adsero-optima.com
      *  Original TEMPLATE author: Tommy Atkins - Chief Development Officer
      *
      *  Licensed under the Apache License, Version 2.0 (the "License");
      *  you may not use this file except in compliance with the License.
      *  You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
      *
      *  Unless required by applicable law or agreed to in writing, software
      *  distributed under the License is distributed on an "AS IS" BASIS,
      *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
      *  See the License for the specific language governing permissions and
      *  limitations under the License.
      *
      *  The above copyright notice and this permission notice shall be included in all
      *  copies or substantial portions of the Software.
      *
      *                      http://www.adsero-optima.com/
      *
      *===============================================================
     h nomain aut(*use) extbinint(*yes) option(*nodebugio) debug
      *===============================================================
     fCUSMSTF   uf a e           k disk    usropn commit rename(CUSMSTFR:R1)
      * ( CUSTNO )====================================================
     d CUSMSTF@        pr              n
     d  Action                        5    const
     d  RecKey                             const options(*nopass) like(RecKey)
      *===============================================================
     d CUSMSTFP        s               *   inz(%addr(Record))   export
     d RecKey          ds                  likerec(R1:*KEY)
     d Record        e ds                  extname(CUSMSTF)     inz
      *===============================================================
     d SaveR           ds                  likerec(R1)
      *===============================================================
      /copy SRCCPY,ERR03#                        Move *DIAG and Resend *ESCAPE Messages
      /copy SRCCPY,ERR11#                        Send *ESCAPE Message
      *===============================================================
     d PUT             pr              n
     d UPD             pr              n
     d DLT             pr              n
     d GET             pr              n
     d OPNCLO          pr              n
      *===============================================================
     d Action          ds             5
     d  A1                            1
     d  A2                            1
      *===============================================================
     d NoKeys          s              1
      *===============================================================
      ****************************************************************
      *===============================================================
     p CUSMSTF@        b                   export
     d                 pi              n
     d  A                             5    const
     d  K                                  const options(*nopass) like(RecKey)
      *=====================================================
      /free
       monitor;
          //================================================
          Action = A;
          //================================================
          if %parms = 2;
             RecKey = K;
          else;
             eval-corr RecKey = Record;
          endif;
          //================================================
          select;
              when A1 = 'I';   // Insert
                   return PUT();
              when A1 = 'U';   // Update
                   return UPD();
              when A1 = 'D';   // Deletion
                   return DLT();
              when A1 = 'R';   // Read
                   return GET();
              when A1 = 'F';   // File Open/Close
                   return OPNCLO();
              other;
                   ERR11('IOS9999':'ERRMSGF':'CUSMSTF@  '+Action);
          endsl;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      ****************************************************************
      *===============================================================
     p PUT             b
     d                 pi              n
      *=====================================================
      /free
       monitor;
          //================================================
          select;
              when A2=' ';
                   RecKey.CUSTNO = *hival;
                   setgt %kds(RecKey) R1;
                   readp(n) R1 SaveR;
                   if %eof;
                       CUSTNO = 1;
                   else;
                       CUSTNO = SaveR.CUSTNO + 1;
                   endif;
              when A2 = 'N';
                   // Number supplied in Record. Don't Generate
          endsl;
          //================================================
          write  R1;
          return *on;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      ****************************************************************
      *===============================================================
     p UPD             b
     d                 pi              n
      *=====================================================
      /free
       monitor;
          //================================================
          chain %kds(RecKey) R1 SaveR;
          //================================================
          select;
              when not %found and A2=' ';
                   ERR11('IOS0010':'ERRMSGF':'CUSMSTF@  '+Action);
              when not %found and A2='I';
                   A2=' ';
                   return PUT();
              when not %found and A2='N';
                   return PUT();
              when %found;
                   update R1;
                   return *on;
          endsl;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      ****************************************************************
      *===============================================================
     p DLT             b
     d                 pi              n
      *=====================================================
      /free
       //===================================================
       monitor;
          //================================================
          delete %kds(RecKey) R1;
          return *on;
          //================================================
       on-error;
          ERR03();
       endmon;
       //===================================================
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      ****************************************************************
      *===============================================================
     p GET             b
     d                 pi              n
      *=====================================================
      /free
       //===================================================
       monitor;
          //================================================
          select;
              when A2 = ' ';  // Read - All Keys
                   chain(n) %kds(RecKey) R1 ;
                   return %found;
              when A2 = 'F';  // Read Next Record
                   read(n)  R1 ;
              when A2 = 'B';  // Read Previous Record
                   readp(n) R1 ;
             other;
                   ERR11('IOS9999':'ERRMSGF':'CUSMSTF@  '+Action);
          endsl;
          //================================================
          return %eof;
          //================================================
       on-error;
          ERR03();
       endmon;
       //===================================================
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      ****************************************************************
      *===============================================================
     p OPNCLO          b
     d                 pi              n
      *=====================================================
      /free
       //===================================================
       monitor;
          //================================================
          select;
              when A2 = 'O';      // Open/Initialize
                   if not %open(CUSMSTF);
                           open CUSMSTF;
                   endif;
              when A2 = 'C';      // Close
                   if %open(CUSMSTF);
                      close CUSMSTF;
                   endif;
                   *inlr = *on;
              other;
                   ERR11('IOS9999':'ERRMSGF':'AOF520F$  '+Action);
          endsl;
          //================================================
          return '1';
          //================================================
       on-error;
          ERR03();
       endmon;
       //===================================================
      /end-free
      *=====================================================
     p                 e
      *===============================================================

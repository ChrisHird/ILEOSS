      *===============================================================
      *===============================================================
      *
      *  Copyright (c) 2008-2018 TEMBO Technology Lab (Pty) Ltd.
      *  Created by AO Foundation - www.adsero-optima.com
      *  Original TEMPLATE author: Tommy Atkins - Chief Development Officer
      *
      *  Licensed under the Apache License, Version 2.0 (the "License");
      *  you may not use this file except in compliance with the License.
      *  You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
      *
      *  Unless required by applicable law or agreed to in writing, software
      *  distributed under the License is distributed on an "AS IS" BASIS,
      *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
      *  See the License for the specific language governing permissions and
      *  limitations under the License.
      *
      *  The above copyright notice and this permission notice shall be included in all
      *  copies or substantial portions of the Software.
      *
      *                      http://www.adsero-optima.com/
      *
      *===============================================================
     h nomain aut(*use)
      *===============================================================
     fQSYSPRT   o    f  198        printer usropn oflind(*inof)
      *===============================================================
      /copy SRCCPY,API_EC#                       API Error Data Structure
      /copy SRCCPY,ERR03#                        Move *DIAG and Resend *ESCAPE Messages
      *===============================================================
     d GETSYS          pr             8
      /copy SRCCPY,CMD#                          Execute Command - Processor
      *===============================================================
     d Initialize      pr
     d  WEBSRVCFGP                     *         const
     d PrintAudit      pr
      *===============================================================
     d ParmLP          s               *   inz(%addr(ParmL)) export
     d ParmL           ds                  qualified
     d  Applic                         *
     d  InpPtr                         *
     d  OutPtr                         *
     d  QueCtl                       20
     d  InpLen                       10i 0
     d  OutLen                       10i 0
      *===============================================================
     d W               ds                  export qualified
     d  ProgName                     10
     d  ProgDescr                    50
     d  Audit                          n
     d  Handle                        6
     d  SysName                       8
      *===============================================================
     d KV              ds                  export qualified
     d  Key                         256    varying
     d  Value                       256    varying
      *===============================================================
     d QWCRNETA        pr                  extpgm('QWCRNETA')
     d  RV                           20
     d  RVL                          10i 0 const
     d  NoAttr                       10i 0 const
     d  Attr                         10    const
     d  Err                                like(EC)
      ***************
     d RVP             s               *   inz(%addr(RV))
     d RV              ds            40    qualified
     d  NoAttr                       10i 0
     d  AttrOS                       10i 0
      *===============================================================
      * START: Global Area for AUDIT Procedure
      *===============================================================
     d InzPrint        pr
     d CenterText      pr
     d   Text                        50
      *=====================================================
      * Program Status Data Structure
     d PSDS           sds
     d  Job                  244    253
     d  User                 254    263
     d  JobNo                264    269
      *===============================================================
     d SysCtlDA        ds          2000    DtaAra(SysCtl)
     d  OrgName                      50
      *=====================================================
     d wDate           s             10
     d wTime           s              8
     d PgmNme          s             10
     d Head1           s             50
     d Head2           s             50
     d Head3           s             50
     d EOR             s            198    Inz(*All'*')
     d DashLine        s            198    Inz(*All'-')
      *===============================================================
     d WEBSRVCFGP      s               *   export
     d WEBSRVCFG     e ds                  extname(WEBSRVCFG)    qualified
     d                                       based(WEBSRVCFGP)
      *===============================================================
     d SQL$P           s               *   inz(%addr(SQL$)) export
     d SQL$            s           4096    varying
      *===============================================================
     d InpDataP        s               *   export
     d InpData         s           4096    based(InpDataP)
     d OutDataP        s               *   export
     d OutData         s           4096    based(OutDataP)
      *===============================================================
     d PrintLineP      s               *
     d PrintLine       s            198    based(PrintLineP)
     d PrintLineW      s            198
     d PrintLen        s             10i 0
      *===============================================================
      * Output Specifications <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     oQSYSPRT   e            #HDG1            01
     o         or    of
     o                                          + 0 'User:'
     o                       User               + 1
     o                       Head1              124
     o                                          187 'Date:'
     o                       wDate              + 1
      *---------------------------------------------------------------
     o          e            #HDG1       1
     o         or    of
     o                                          + 0 ' Job:'
     o                       Job                + 1
     o                       Head2              124
     o                                          187 'Time:'
     o                       wTime              198
      *---------------------------------------------------------------
     o          e            #HDG1       1
     o         or    of
     o                                          + 0 ' Pgm:'
     o                       PgmNme             + 1
     o                       Head3              124
     o                       Page          4    198
     o                                          187 'Page:'
      *---------------------------------------------------------------
     o          e            #HDG1       1
     o         or    of
     o                       EOR                + 0
      *---------------------------------------------------------------
     o          ef           #AUDIT      1
     o                       PrintLine          + 0
      *---------------------------------------------------------------
     o          ef           #AUDITW     1
     o                       PrintLineW         + 0
      *---------------------------------------------------------------
     o          e            #EOR        1
     o                       EOR              +   0
      *===============================================================
      * END: Global Area for AUDIT Procedure
      *===============================================================
      *& Retrieve System Name <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     p GETSYS          b                   export
     d                 pi             8
      *=====================================================
     d NAP             s               *
     d NA              ds                  based(NAP)
     d  Attr                         10
     d  DataT                         1
     d  InfSts                        1
     d  DataL                        10i 0
     d  Data                          8
      *=====================================================
      /free
       monitor;
          QWCRNETA(RV:40:1:'SYSNAME':EC);
          NAP = RVP + RV.AttrOS;
          return Data;
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      *& Execute Command <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     p Cmd             b                   export
     d                 pi
     d  Cmd$                       4096    const varying options(*varsize)
      *=====================================================
     d QCMDEXC         pr                  extpgm('QCMDEXC')
     d                             4096    const options(*varsize)
     d                               15p 5 const
      *=====================================================
      /free
       //===================================================
       monitor;
          //================================================
          QCMDEXC(Cmd$:%len(Cmd$));
          //====ParmL=======================================
       on-error;
          ERR03();
       endmon;
      /end-free
     p                 e
      *===============================================================
      *& Initialize <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     p Initialize      b                   export
     d                 pi
     d  WebCfgPtr                      *   const
      *=====================================================
      /free
       monitor;
          //================================================
          clear ParmL;
          clear W;
          W.SysName = GetSys();
          //================================================
          WEBSRVCFGP = WebCfgPtr;
          //========================================
          if WEBSRVCFG.WSPRTA = 'Y';
             W.Audit = *on;
          endif;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
     p                 e
      *===============================================================
      *& Print Audit Report <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     p PrintAudit      b                   export
      *=====================================================
      /free
       monitor;
          //===============================================
          select;
              when not W.Audit and not %open(QSYSPRT);
                   return;
              when not W.Audit and %open(QSYSPRT);
                   %Subst(EOR:92:15) = ' End-of-Report ';
                   except #EOR;
                   close QSYSPRT;
              when W.Audit and not %open(QSYSPRT);
                   InzPrint();
          endsl;
          //===============================================
          PrintLineW = '   Time Stamp: ' + %Char(%TimeStamp());
          Except #AUDITW;
          PrintLineW = 'Queue Control: ' + ParmL.QueCtl;
          Except #AUDITW;
          //================================================
          PrintLineW = 'Input: Len = '+%char(ParmL.InpLen);
          Except #AUDITW;
          //================================================
          PrintLineP = ParmL.InpPtr;
          PrintLen   = ParmL.InpLen;
          //================================================
          dow PrintLen > 0;
              except #AUDIT;
              PrintLineP += 198;
              PrintLen   -= 198;
          enddo;
          //================================================
          PrintLineP = %addr(SQL$:*data);
          PrintLen   = %len(SQL$);
          //================================================
          dow PrintLen > 0;
              except #AUDIT;
              PrintLineP += 198;
              PrintLen   -= 198;
          enddo;
          //================================================
          PrintLineW = 'Output: Len = '+%char(ParmL.OutLen);
          Except #AUDITW;
          //================================================
          PrintLineP = ParmL.OutPtr;
          PrintLen   = ParmL.OutLen;
          //================================================
          dow PrintLen > 0;
              except #AUDIT;
              PrintLineP += 198;
              PrintLen   -= 198;
          enddo;
          //================================================
          PrintLineW = DashLine;
          Except #AUDITW;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *===============================================================
      * Initialize Print File <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *===============================================================
     p InzPrint        b
      *=====================================================
      /free
       monitor;
          //================================================
          in SysCtlDA;           //?System Control Data Area;
          Head1 = OrgName;
          CenterText(Head1);
          wDate = %Char(%date():*ISO);
          //================================================
          Head2 = W.ProgDescr;
          CenterText(Head2);
          wTime = %Char(%time():*ISO);
          //================================================
          PgmNme = W.ProgName;
          Head3 = 'System: ' + %Trim(GetSys());
          CenterText(Head3);
          //================================================
          Cmd('OVRPRTF FILE(QSYSPRT) PAGESIZE(72 198) CPI(15) OVRFLW(69)' +
              ' MAXRCDS(*NOMAX) USRDTA('+%trim(W.ProgName)+')');
          //================================================
          Open QSYSPRT;
          Except #HDG1;
          //================================================
          PrintLineW = 'WEBSRVCFG:' +
                      ' WSCODE="' + %trim(WEBSRVCFG.WSCODE) + '"' +
                      ' WSENV="'  + %trim(WEBSRVCFG.WSENV)  + '"' +
                      ' WSDESC="' + %trim(WEBSRVCFG.WSDESC) + '"' +
                      ' WSAVL="'  + %trim(WEBSRVCFG.WSAVL)  + '"' +
                      ' WSSYNC="' + %trim(WEBSRVCFG.WSSYNC) + '"';
          Except #AUDITW;
          //================================================
          PrintLineW = DashLine;
          Except #AUDITW;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
      *==============================================================
      * CenterText <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
      *==============================================================
     p CenterText      b
     d                 pi
     d  Text                         50
      *=====================================================
     d Len             s              5i 0
     d Pos             s              5i 0
     d Work            s             50
      *=====================================================
      /free
       monitor;
          //================================================
          Len = %len(%trim(Text));
          //================================================
          if Len = 0;
             return;
          else;
             Work = Text;
             Pos  = (50 - Len)/2;
             clear Text;
             %Subst(Text:Pos:Len) = Work;
          endif;
          //================================================
       on-error;
          ERR03();
       endmon;
      /end-free
      *=====================================================
     p                 e
     ?*=========================================================================*
